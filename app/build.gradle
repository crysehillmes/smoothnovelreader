apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'me.tatarka.retrolambda'


android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolsVersion

    productFlavors {  }

    defaultConfig {
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        applicationId 'org.cryse.novelreader'
        versionCode appVersionCode
        versionName appVersionName
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {}
        release {}
    }

    buildTypes {
        debug {
            debuggable true
            shrinkResources true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
}

dependencies {
    // Modules
    compile project(':main_module')
    compile project(':novel_sources:baidu_novel_source')
    compile project(':changelog')

    // Android Support Libraries
    compile "com.android.support:support-v13:$supportlibraryVersion"
    compile "com.android.support:gridlayout-v7:$supportlibraryVersion"
    compile "com.android.support:appcompat-v7:$supportlibraryVersion"
    compile "com.android.support:design:$supportlibraryVersion"
    compile "com.android.support:support-annotations:$supportlibraryVersion"
    compile "com.android.support:cardview-v7:$supportlibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportlibraryVersion"

    // ReactiveX
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'io.reactivex:rxjava-async-util:0.21.0'

    // REST Client
    // compile 'com.squareup.retrofit:retrofit:1.7.0'

    compile 'com.umeng.analytics:analytics:5.5.3'

    // Dependency Injection
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton:butterknife:6.1.0'

    // Utils
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile ('com.crashlytics.android:crashlytics:1.+') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
        exclude group: 'com.squareup.okhttp', module: 'okhttp-urlconnection'
    }

    retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:$retrolambdaVersion"

    // UI elements
    compile ('com.navercorp.pulltorefresh:library:3.2.3@aar') {
        exclude module: 'support-v4'
    }
    compile ('com.navercorp.pulltorefresh:extra-viewpager:3.2.3@aar') {
        exclude module: 'support-v4'
    }
    compile 'com.quentindommerc.superlistview:library:1.5.2'
    compile 'com.afollestad:material-dialogs:0.7.4.1'
    compile('com.mikepenz:materialdrawer:3.0.4') {
        transitive = true
        exclude module: 'appcompat-v7'
    }
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.cocosw:bottomsheet:1.1.1'
    compile 'me.zhanghai.android.materialprogressbar:library:1.0.2'

    compile 'org.apache.tika:tika-core:1.8'
    compile 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // Local libs
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

retrolambda {
    jdk System.getenv('JAVA8_HOME')
    oldJdk System.getenv('JAVA7_HOME')
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}

def Properties props = new Properties()
def propFile = new File('app/signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.debug.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.debug.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.debug.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.debug.keyPassword = props['KEY_PASSWORD']
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.debug.signingConfig = null
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'signing.properties not found'
    android.buildTypes.debug.signingConfig = null
    android.buildTypes.release.signingConfig = null
}