apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
//apply plugin: 'com.google.gms.google-services'


android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolsVersion

    productFlavors {  }

    defaultConfig {
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        applicationId 'org.cryse.novelreader'
        versionCode appVersionCode
        versionName appVersionName
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {}
        release {}
    }

    buildTypes {
        debug {
            debuggable true
            shrinkResources true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
}

dependencies {
    // Modules
    compile project(':main_module')
    compile project(':novel_sources:novel_source_baidubrowser')
    compile project(':novel_sources:novel_source_easou')
    compile project(':changelog')

    // Android Support Libraries
    compile libDependencies.support_v13
    compile libDependencies.gridlayout_v7
    compile libDependencies.appcompat
    compile libDependencies.design
    compile libDependencies.cardview
    compile libDependencies.recyclerview
    // compile libDependencies.play_services_analytics

    // ReactiveX
    compile libDependencies.rxjava
    compile libDependencies.rxandroid

    // REST Client
    // compile 'com.squareup.retrofit:retrofit:1.7.0'

    compile libDependencies.easypermission

    compile libDependencies.umeng_analytics

    // Dependency Injection
    compile libDependencies.dagger
    apt libDependencies.dagger_compiler
    compile libDependencies.butterknife

    // Utils
    compile libDependencies.timber
    compile libDependencies.glide

    retrolambdaConfig libDependencies.retrolambda

    // UI elements
    compile (libDependencies.pulltorefresh) {
        exclude module: 'support-v4'
    }

    compile (libDependencies.pulltorefresh_viewpager) {
        exclude module: 'support-v4'
    }

    compile libDependencies.superlistview

    compile(libDependencies.materialdialog) {
        transitive = true
    }

    compile(libDependencies.materialdialog_commons) {
        transitive = true
    }

    compile(libDependencies.materialdrawer) {
        transitive = true
        exclude module: 'appcompat-v7'
    }

    compile libDependencies.materialprogressbar

    compile libDependencies.tika
    compile libDependencies.juniversalchardet
    compile libDependencies.persistentsearchview
    compile libDependencies.superrecyclerview
    compile libDependencies.re2j
    compile libDependencies.bottomsheet
    compile(libDependencies.crashlytics) {
        transitive = true;
    }
    debugCompile libDependencies.leakcanary
    releaseCompile libDependencies.leakcanary_noop
    debugCompile libDependencies.crashwoodpecker
    releaseCompile libDependencies.crashwoodpecker_noop

    // Local libs
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

retrolambda {
    jdk System.getenv('JAVA8_HOME')
    oldJdk System.getenv('JAVA7_HOME')
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}

def Properties props = new Properties()
def propFile = new File('app/signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.debug.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.debug.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.debug.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.debug.keyPassword = props['KEY_PASSWORD']
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.debug.signingConfig = null
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'signing.properties not found'
    android.buildTypes.debug.signingConfig = null
    android.buildTypes.release.signingConfig = null
}